# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-oricutron
pkgver=1.2.4
_pkgver="$(echo $pkgver | sed -r 's/[.]+/_/g')"
pkgrel=1
pkgdesc="Tangerine Computer Systems Oric-1, Atmos, Stratos, Telestrat & Pravetz 8D Emulator"
arch=('x86_64')
url="http://www.petergordon.org.uk/oricutron"
license=('GPL2')
depends=(
  'gtk3'
  'sdl2'
)
makedepends=('cmake')
source=(
  "https://github.com/pete-gordon/oricutron/archive/V$_pkgver.tar.gz"
  "01_Fix_CMake_Lists.patch"
)
sha256sums=(
  'c4bf6570cf9571baec917ee1df8b5f44d5641c9334cc32af0472809a4cca4a7d'
  '9141cdcf39efac3009270c849b20bb5e9b7606421c9a6c6b512999cf33506015'  
)
_installdir="/opt/archrgs/emulators/$pkgname"

prepare() {
  cd "oricutron-$_pkgver"

  patch -Np1 -i ../01_Fix_CMake_Lists.patch

  if [[ -d ./build ]]; then
    rm -rf ./build
  fi
  mkdir ./build
}

build() {
  cd "oricutron-$_pkgver/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}"
  make 
}

package() {
  cd "oricutron-$_pkgver"

  install -Dm755 ./build/Oricutron -t "${pkgdir}${_installdir%/}/bin"
  install -Dm755 ./oricutron.cfg -t "${pkgdir}${_installdir%/}"
  install -dm755 "${pkgdir}${_installdir%/}"/{disks,images,roms}
  cp -r ./{disks,images,roms} "${pkgdir}${_installdir%/}"
}

