# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-vice
pkgver=3.5
pkgrel=1
pkgdesc="Commodore C64, C64DTV, C128, VIC20, PET, PLUS4 & CBM-II Emulator"
arch=('x86_64')
license=('GPL2')
url="https://vice-emu.sourceforge.io"
depends=(
  'alsa-lib'
  'libpulse'
  'pciutils'
  'portaudio'
  'sdl2_image'
)
makedepends=(
  'dos2unix'
  'ffmpeg'
  'libpcap' 
  'libxaw'
  'subversion'
  'xa'
  'xorg-bdftopcf'
  'xorg-mkfontdir'
)
source=("https://sourceforge.net/projects/vice-emu/files/releases/vice-$pkgver.tar.gz/download")
sha256sums=('56b978faaeb8b2896032bd604d03c3501002187eef1ca58ceced40f11a65dc0e')
_installdir="/opt/archrgs/emulators/$pkgname"

build() {
  cd "vice-$pkgver"

  ./autogen.sh
  ./configure \
    --prefix="${_installdir%/}" \
    --enable-external-ffmpeg \
    --enable-sdlui2 \
    --enable-x64 \
    --without-oss \
    --enable-ethernet
  make
}

package() {
  cd "vice-$pkgver"

  make DESTDIR="${pkgdir}" install
}

