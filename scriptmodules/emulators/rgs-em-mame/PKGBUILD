# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-mame
pkgver=0.234
pkgrel=1
pkgdesc="Multiple Arcade Machine Emulator"
url="https://mamedev.org"
license=('custom:MAME')
arch=('x86_64')
depends=(
  'flac'
  'libpulse'
  'libutf8proc'
  'portaudio'
  'portmidi'
  'pugixml'
  'qt5-base'
  'sdl2_ttf'
)
makedepends=(
  'glm'
  'libxinerama'
  'nasm'
  'python'
  'rapidjson'
)
source=("https://github.com/mamedev/mame/archive/mame${pkgver/./}.tar.gz")
sha256sums=('c6b464f33b9e0eb7afb528fac52619b073525ded86f35afc36cca4dd0363fe4d')
_installdir="/opt/archrgs/emulators/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

prepare() {
  cd "mame-mame${pkgver/./}"

  ##Use System Libraries
  sed -e 's|\# USE_SYSTEM_LIB|USE_SYSTEM_LIB|g' -i makefile
  ##Except for ASIO and Lua
  sed -e 's|USE_SYSTEM_LIB_LUA|\# USE_SYSTEM_LIB_LUA|g' -i makefile
  sed -e 's|USE_SYSTEM_LIB_ASIO|\# USE_SYSTEM_LIB_ASIO|g' -i makefile
  ##Rename The BUILDDIR Variable To Avoid a Conflict With The Makepkg Variable of the Same Name
  sed -e 's|BUILDDIR|BLDDIR|g' -i makefile
}

build() {
  cd "mame-mame${pkgver/./}"

  export CFLAGS+=" -I/usr/include/lua5.3/"
  export CXXFLAGS+=" -I/usr/include/lua5.3/"

  ##Hack to Force Linking to lua5.3
  mkdir ./lib
  ln -s /usr/lib/liblua5.3.so ./lib/liblua.so
  export LDFLAGS+=" -L${PWD}/lib"

  make \
    ARCHOPTS=-flifetime-dse=1 \
    CONFIG=release \
    NOWERROR=1 \
    OPTIMISE=2 \
    PREFIX="${_installdir%/}" \
    PTR64=1 \
    PYTHON_EXECUTABLE=python \
    TARGETOS=linux \
    TOOLS=1
}

package() {
  cd "mame-mame${pkgver/./}"

  _BIN=(
    'castool'
    'chdman'
    'imgtool'
    'jedutil'
    'ldresample'
    'ldverify'
    'mame'
    'nltool'
    'nlwav'
    'pngcmp'
    'regrep'
    'romcmp'
    'split'
    'srcclean'
    'testkeys'
    'unidasm'
)

  ##Install Binaries
  for bin in "${_BIN[@]}"; do
    install -Dm755 "$bin" -t "${pkgdir}${_installdir%/}"
  done

  ##Install Data
  cp -ar ./{artwork,bgfx,docs,plugins,language,ctrlr,keymaps,hash} "${pkgdir}${_installdir%/}"
  install -Dm644 ./COPYING "${pkgdir}${_licencedir%/}/LICENCE"
}

