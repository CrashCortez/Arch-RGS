# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-scummvm
pkgver=2.2.0
pkgrel=1
pkgdesc="Virtual Machine for Graphical Point-and-Click Adventure Games"
arch=('x86_64')
license=('GPL2')
url="http://www.scummvm.org"
depends=(
  'a52dec'
  'faad2'
  'flac'
  'fluidsynth'
  'glew'
  'gtk3'
  'libjpeg-turbo'
  'libmad'
  'libmpeg2'
  'libtheora'
  'libspeechd'
  'sdl2_net'
)
makedepends=(
  'git'
  'mesa'
)
source=("https://downloads.scummvm.org/frs/scummvm/$pkgver/scummvm-$pkgver.tar.xz")
sha256sums=('1469657e593bd8acbcfac0b839b086f640ebf120633e93f116cab652b5b27387')
_installdir="/opt/archrgs/emulators/$pkgname"

build() {
  cd "scummvm-$pkgver"

  ./configure \
    --prefix="${_installdir%/}" \
    --enable-all-engines \
    --enable-c++11 \
    --enable-release
  make
}

package() {
  cd "scummvm-$pkgver"

  make DESTDIR="${pkgdir}" install
}

