# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-scummvm
pkgver=2.3.0.r10055.04c6113655
pkgrel=1
pkgdesc="Virtual Machine for Graphical Point-and-Click Adventure Games"
arch=('x86_64')
license=('GPL2')
url="http://www.scummvm.org"
depends=(
  'a52dec'
  'faad2'
  'flac'
  'fluidsynth'
  'glew'
  'gtk3'
  'libjpeg-turbo'
  'libmad'
  'libmpeg2'
  'libtheora'
  'libspeechd'
  'sdl2_net'
)
makedepends=(
  'git'
  'mesa'
)
source=("$pkgname::git+https://github.com/scummvm/scummvm")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "%s" "$(git describe --long | sed 's/^desc\///; s/git//g; s/\([^-]*-\)g/r\1/; s/-/./g')"
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix="${_installdir%/}" \
    --enable-all-engines \
    --enable-c++11 \
    --enable-release
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="${pkgdir}" install
}

