# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-retroarch
pkgver=1.9.7
pkgrel=1
pkgdesc="Frontend for the Libretro API"
arch=('x86_64')
url="http://www.libretro.com"
license=('GPL3')
depends=(
  'ffmpeg'
  'flac'
  'libass'
  'libcaca'
  'libxinerama'
  'libxrandr'
  'mbedtls'
  'miniupnpc'
  'openal'
  'python'
  'qt5-base'
)
makedepends=(
  'git'
  'libx11'
  'libxcb'
  'libxext'
  'libxkbcommon'
  'libxv'
  'libxxf86vm'
  'vulkan-headers'
  'vulkan-icd-loader'
  'wayland'
  'wayland-protocols'
)
source=(
  "retroarch::https://github.com/libretro/RetroArch/archive/refs/tags/v$pkgver.tar.gz"
  "coreinfo::https://github.com/libretro/libretro-core-info/archive/refs/tags/v$pkgver.tar.gz"
  "01_disable_search.patch"
  "02_shader_path_config_enable.patch"
  "03_revert_default_save_paths.patch"
)
sha256sums=(
  'a4a1791dbff796dccfb377f9a61bd4b1bea4f754ab6611f51fa1d0466babbc9e'
  '027dc9efb075964dbd2c0df6ab4a3b68c7ac1ae905426ef3f1a6aee5b2a8b8a8'
  'bde21cb72a614d6cf3f79e80aad35b26ac80d15e0997fe99e68d665c71f58025'
  '44ed90547e36dad0d8891e1d13ac236cc6cb34d7ff03d9e5c6c93b8c4cfdbf34'
  'da39da8ed681d4747bccb61db5badc626ffcf8964bb5b6ab36ca8100a21ed97c'
)
_installdir="/opt/archrgs/emulators/$pkgname"

prepare() {
  cd "RetroArch-$pkgver"

  patches=(
    '01_disable_search.patch'
    '02_shader_path_config_enable.patch'
    '03_revert_default_save_paths.patch'
)

  for patch in "${patches[@]}"; do
    patch -Np1 -i ../"$patch"
  done
}

build() {
  cd "RetroArch-$pkgver"

  ./configure \
    --prefix="${_installdir%/}" \
    --disable-builtinflac \
    --disable-builtinmbedtls \
    --disable-builtinminiupnpc \
    --disable-builtinzlib \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-sdl \
    --enable-dbus
  make
  make -C ./libretro-common/audio/dsp_filters
  make -C ./gfx/video_filters
}

package() {
  cd "RetroArch-$pkgver"

  make DESTDIR="${pkgdir}" install

  install -Dm644 ./libretro-common/audio/dsp_filters/*.{dsp,so} -t "${pkgdir}${_installdir%/}/filters/audio"
  install -Dm644 ./gfx/video_filters/*.{filt,so} -t "${pkgdir}${_installdir%/}/filters/video"

  cd "../libretro-core-info-$pkgver"

  install -Dm644 ./*.info -t "${pkgdir}${_installdir%/}/info"
}

