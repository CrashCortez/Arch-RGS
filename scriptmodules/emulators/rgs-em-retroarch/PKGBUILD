# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-retroarch
pkgver=1.9.1
pkgrel=1
pkgdesc="Frontend for the Libretro API"
arch=('x86_64')
url="http://www.libretro.com"
license=('GPL3')
depends=(
  'ffmpeg'
  'flac'
  'libass'
  'libcaca'
  'libxinerama'
  'libxrandr'
  'mbedtls'
  'miniupnpc'
  'openal'
  'python'
  'qt5-base'
)
makedepends=(
  'git'
  'libx11'
  'libxcb'
  'libxext'
  'libxkbcommon'
  'libxv'
  'libxxf86vm'
  'vulkan-headers'
  'vulkan-icd-loader'
  'wayland'
  'wayland-protocols'
)
source=(
  "$pkgname::git+https://github.com/libretro/RetroArch#tag=v$pkgver"
  "core-info::git+https://github.com/libretro/libretro-core-info#tag=v$pkgver"
  "01_disable_search.patch"
  "02_shader_path_config_enable.patch"
  "03_revert_default_save_paths.patch"
)
sha256sums=(
  'SKIP'
  'SKIP'
  '8b585ac2afdd4b158ed840237c1736f8a76b10eda77e29e2638615416d38674c'
  '115921fadc51f8fc742209921ec04e04618987eecc2f39ee8ed7e90f71ddeab4'
  '4ec07105d2326dd8eeb06d915ba4177f947691991835504c3b9b738813edf91e'
)
_installdir="/opt/archrgs/emulators/$pkgname"

prepare() {
  cd "$pkgname"

  patch -Np1 -i ../01_disable_search.patch
  patch -Np1 -i ../02_shader_path_config_enable.patch
  patch -Np1 -i ../03_revert_default_save_paths.patch
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix="${_installdir%/}" \
    --disable-builtinflac \
    --disable-builtinmbedtls \
    --disable-builtinminiupnpc \
    --disable-builtinzlib \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-sdl \
    --enable-dbus
  make
  make -C ./libretro-common/audio/dsp_filters
  make -C ./gfx/video_filters
}

package() {
  cd "$pkgname"

  make DESTDIR="${pkgdir}" install

  install -Dm644 ./libretro-common/audio/dsp_filters/*.{dsp,so} -t "${pkgdir}${_installdir%/}/filters/audio"
  install -Dm644 ./gfx/video_filters/*.{filt,so} -t "${pkgdir}${_installdir%/}/filters/video"
  install -Dm644 ../core-info/*.info -t "${pkgdir}${_installdir%/}/info"
}

