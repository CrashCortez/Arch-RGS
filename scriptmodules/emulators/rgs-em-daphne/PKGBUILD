# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-daphne
pkgver=r130.3b30614
pkgrel=1
pkgdesc="Arcade Laserdisc Emulator"
arch=('x86_64')
url="http://www.daphne-emu.com"
license=('GPL2')
depends=(
  'glew'
  'libogg'
  'libvorbis'
  'sdl_image'
  'sdl_ttf'
)
makedepends=('git')
source=("$pkgname::git+https://github.com/DavidGriffith/daphne")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$pkgname/src/vldp2"

  ./configure \
    --prefix="${_installdir%/}" \
    --disable-accel-detect
  make -f Makefile.linux_x64

  cd "$srcdir/$pkgname/src/game/singe"

  make -f Makefile.linux_x64

  cd "$srcdir/$pkgname/src"

  ln -s Makefile.vars.linux_x64 Makefile.vars
  make
}

package() {
  cd "$pkgname"

  install -Dm755 ./daphne.bin "${pkgdir}${_installdir%/}/bin/daphne"
  install -Dm644 ./*.so -t "${pkgdir}${_installdir%/}/lib"
  install -dm755 "${pkgdir}${_installdir%/}/share"
  cp -r ./{doc,pics,sound} "${pkgdir}${_installdir%/}/share"
}

