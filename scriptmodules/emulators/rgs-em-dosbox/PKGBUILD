# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-dosbox
pkgver=0.74.2.r4390
pkgrel=1
pkgdesc="DOS Emulator"
arch=('x86_64')
url="http://dosbox.sourceforge.net"
license=('GPL2')
depends=(
  'alsa-lib'
  'libglvnd'
  'libpng'
  'sdl_net'
  'sdl_sound'
)
makedepends=(
  'mesa-libgl'
  'subversion'
)
source=("$pkgname::svn+https://svn.code.sf.net/p/dosbox/code-0/dosbox/trunk")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"

  printf %s "$(sed 's/-/./' ./VERSION).r$(svnversion ./)"
}

build() {
  cd "$pkgname"

  ./autogen.sh
  ./configure \
    --prefix="${_installdir%/}"
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="${pkgdir}" install
}

