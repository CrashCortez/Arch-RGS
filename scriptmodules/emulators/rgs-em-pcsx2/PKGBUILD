# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-pcsx2
pkgver=1.7.0.r1288.b5a917fb0
pkgrel=1
pkgdesc="Sony PlayStation 2 Emulator"
arch=('x86_64')
url="http://www.pcsx2.net"
license=('GPL2')
depends=(
  'fmt'
  'libaio'
  'portaudio'
  'sdl2'
  'soundtouch'
  'wxgtk3'
)
makedepends=(
  'cmake'
  'git'
  'png++'
)
source=("$pkgname::git+https://github.com/PCSX2/pcsx2")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "%s" "$(git describe --tags | sed 's/^v//; s/-dev//; s/-/.r/; s/-g/./')"
}

prepare() {
  cd "$pkgname"

  if [[ -d ./build ]]; then
    rm -rf ./build
  fi
  mkdir ./build

  git submodule update --init --recursive
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_BUILD_RPATH_USE_ORIGIN=ON \
    -DENABLE_TESTS=OFF \
    -DDOC_DIR=/doc \
    -DGAMEINDEX_DIR=/share \
    -DPLUGIN_DIR=/lib \
    -DDISABLE_BUILD_DATE=ON \
    -DDISABLE_PCSX2_WRAPPER=ON \
    -DEXTRA_PLUGINS=ON \
    -DSDL2_API=ON \
    -DPACKAGE_MODE=ON \
    -DREBUILD_SHADER=ON \
    -DUSE_LTO=ON \
    -DUSE_VTUNE=OFF \
    -DUSE_SYSTEM_YAML=OFF \
    -DXDG_STD=ON \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 \
    -Wno-dev
  make
}

package() {
  cd "$pkgname/build"

  make DESTDIR="${pkgdir}" install
}

