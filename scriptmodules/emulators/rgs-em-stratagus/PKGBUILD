# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-stratagus
pkgver=3.1.0
pkgrel=1
pkgdesc="Strategy Game Engine"
arch=('x86_64')
url="https://wargus.github.io/stratagus"
license=('GPL2')
depends=(
  'libmng'
  'libtheora'
  'sdl2_image'
  'sdl2_mixer'
  'sqlite'
  'tolua++'
)
makedepends=(
  'cmake'
  'doxygen'
  'git'
)
source=("https://github.com/Wargus/stratagus/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('19a367a6c5695c4602d59e0963cbcd9df154ea2d300000aac52426957f64e56a')
_installdir="/opt/archrgs/emulators/$pkgname"

prepare() {
  cd "stratagus-$pkgver"

  if [[ -d ./build ]]; then
    rm -rf ./build
  fi
  mkdir ./build
}

build() {
  cd "stratagus-$pkgver/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DLUA_INCLUDE_DIR=/usr/include/lua5.1 \
    -DCMAKE_CXX_FLAGS="-Wno-error"
  make 
}

package() {
  cd "stratagus-$pkgver/build"

  make DESTDIR="${pkgdir}" install
}

