# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-stratagus
pkgver=2.4.3.r200.72f8ea9e7
pkgrel=1
pkgdesc="Strategy Game Engine"
arch=('x86_64')
url="https://wargus.github.io/stratagus"
license=('GPL2')
depends=(
  'libmng'
  'libtheora'
  'sdl2_image'
  'sdl2_mixer'
  'sqlite'
  'tolua++'
)
makedepends=(
  'cmake'
  'doxygen'
  'git'
)
source=("$pkgname::git+https://github.com/Wargus/stratagus")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "%s" "$(git describe --tags | sed 's/^v//; s/\([^-]*-\)g/r\1/; s/-/./g')"
}

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DLUA_INCLUDE_DIR=/usr/include/lua5.1 \
    -DCMAKE_CXX_FLAGS="-Wno-error"
  make 
}

package() {
  cd "$pkgname/build"

  make DESTDIR="${pkgdir}" install
}

