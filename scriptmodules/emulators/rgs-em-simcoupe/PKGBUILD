# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-simcoupe
pkgver=20210501
pkgrel=1
pkgdesc="SAM Coup√© Emulator"
arch=('x86_64')
url="http://www.simcoupe.org"
license=('GPL2')
depends=(
  'libpng'
  'sdl2' 
)
makedepends=('cmake')
source=("https://github.com/simonowen/simcoupe/archive/$pkgver.tar.gz")
sha256sums=('7addbea2eb2a2e1f50d962ad17e7055d3b40102732737e89f37f4f974b409af4')
_installdir="/opt/archrgs/emulators/$pkgname"

prepare() {
  cd "simcoupe-$pkgver"

  if [[ -d ./build ]]; then
    rm -rf ./build
  fi
  mkdir ./build
}

build() {
  cd "simcoupe-$pkgver/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}"
  make
}

package() {
  cd "simcoupe-$pkgver/build"

  make DESTDIR="${pkgdir}" install
}

