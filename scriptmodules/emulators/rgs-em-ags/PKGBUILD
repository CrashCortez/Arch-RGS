# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-ags
pkgver=3.5.0.27
pkgrel=1
pkgdesc="Graphical Adventure Game Engine"
arch=('x86_64')
url="https://www.adventuregamestudio.co.uk"
license=('custom:Artistic2.0')
depends=( 
  'alsa-lib'
  'jack'
  'libxpm'
  'sdl2'
)
makedepends=(
  'cmake'
  'gcc9'
  'wxgtk'
)
source=("https://github.com/adventuregamestudio/ags/archive/v.$pkgver.tar.gz")
sha256sums=('f8940e390510ebddc872ae032bc65351581a67bf9bb64a0c5d8e996615baf819')
_installdir="/opt/archrgs/emulators/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

prepare() {
  cd ags-v."$pkgver"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd ags-v."$pkgver"/build

  cmake .. \
    -DCMAKE_C_COMPILER=/usr/bin/cc-9 \
    -DCMAKE_CXX_COMPILER=/usr/bin/c++-9 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}"
  make
}

package() {
  cd ags-v."$pkgver"/build

  install -Dm755 ./ags -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ../License.txt "${pkgdir}${_licencedir%/}/LICENSE"
}

