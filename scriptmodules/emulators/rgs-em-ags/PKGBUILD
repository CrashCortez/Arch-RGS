# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-ags
pkgver=3.5.0.32
pkgrel=1
pkgdesc="Graphical Adventure Game Engine"
arch=('x86_64')
url="https://www.adventuregamestudio.co.uk"
license=('custom:Artistic2.0')
depends=( 
  'alsa-lib'
  'jack'
  'libxpm'
  'sdl2'
)
makedepends=(
  'cmake'
  'wxgtk2'
)
source=("$pkgname::git+https://github.com/adventuregamestudio/ags/#tag=v.$pkgver")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

prepare() {
  cd "$pkgname"

  if [[ -d ./build ]]; then
    rm -rf ./build
  fi
  mkdir ./build
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}"
  make
}

package() {
  cd "$pkgname/build"

  install -Dm755 ./ags -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ../License.txt "${pkgdir}${_licencedir%/}/LICENSE"
}

