# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-csp-fm7
pkgver=r4077.601aa7285
pkgrel=1
pkgdesc="Fujitsu FM-7, 77AV & 77AV40 Emulator"
arch=('x86_64')
url="https://github.com/Artanejp/common_source_project-fm7"
license=('GPL2')
depends=(
  'ffmpeg'
  'qt5-base'
  'sdl2'
)
makedepends=(
  'cmake'
  'git'
)
source=("$pkgname::git+https://github.com/Artanejp/common_source_project-fm7")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname/source"

  if [[ -d ./build ]]; then
    rm -rf ./build
  fi
  mkdir ./build

  strings=(
    'include(config_fm16)'
    'include(config_fmr)'
    'include(config_fmtowns)'
    'include(config_casio)'
    'include(config_msx)'
    'include(config_mz80_700)'
    'include(config_pc6001)'
    'include(config_pc8801)'
    'include(config_pc9801)'
    'include(config_x1)'
    'include(config_necmisc)'
    'include(config_toshiba)'
    'include(config_epson)'
    'include(config_sega)'
    'include(config_misccom)'
    'include(config_singleboards)'
)

  for string in "${strings[@]}"; do
    sed -e "s|$string|#$string|g" -i ./CMakeLists.txt
  done
}

build() {
  cd "$pkgname/source/build"

  export CXXFLAGS+=" -ffile-prefix-map=\"$PWD\"=."
  export LDFLAGS+=" -Wl,-rpath='${_installdir%/}/lib'"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_BUILD_RPATH_USE_ORIGIN=ON
  make
}

package() {
  cd "$pkgname/source/build"

  make DESTDIR="${pkgdir}" install
}

