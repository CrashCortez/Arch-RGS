# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-basiliskii
pkgver=r2667.4671258b
pkgrel=1
pkgdesc="Apple Macintosh II Emulator"
arch=('x86_64')
url="http://basilisk.cebix.net"
license=('GPL2')
depends=(
  'gtk2'
  'sdl'
  'vde2'
)
makedepends=('git')
source=(
  "$pkgname::git+https://github.com/cebix/macemu"
  "01_fix_config_rpath_error.patch"
)
sha256sums=(
  'SKIP'
  'a1df34dcb7179657eeeef25a9999c4f38ef97b0fcb7307340809103a7914ec02'
)
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"

  patch -Np1 -i ../01_fix_config_rpath_error.patch
}

build() {
  cd "$pkgname/BasiliskII/src/Unix"

  sed -i "/I_PUSH/d" sshpty.c

  ./autogen.sh \
    --prefix="$_installdir" \
    --enable-sdl-video \
    --enable-sdl-audio \
    --enable-jit-compiler \
    --with-bincue \
    --with-vdeplug
  make LDFLAGS="-Wl,-O1,--sort-common,--as-needed,-z,relro,-z,now"
}

package() {
  cd "$pkgname/BasiliskII/src/Unix"

  make DESTDIR="$pkgdir" install
}

