# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-ppsspp
pkgver=1.10.3.r1123.28da9958e
pkgrel=1
pkgdesc="Sony PSP Emulator"
arch=('x86_64')
url="https://www.ppsspp.org"
license=('GPL2')
depends=(
  'glew'
  'libpng'
  'libzip'
  'sdl2'
  'snappy'
)
makedepends=(
  'cmake'
  'git'
  'libglvnd'
  'python'
  'qt5-tools'
)
source=("$pkgname::git+https://github.com/hrydgard/ppsspp")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "%s" "$(git describe --long | sed 's/^v//; s/\([^-]*-\)g/r\1/; s/-/./g')"
}

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  git submodule update --init --recursive
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DUSE_SYSTEM_LIBZIP=ON \
    -DOpenGL_GL_PREFERENCE=GLVND \
    -DUSE_SYSTEM_SNAPPY=ON
  make
}

package() {
  cd "$pkgname/build"

  install -Dm755 ./PPSSPPSDL -t "${pkgdir}${_installdir%/}/bin"

  cp -dr --no-preserve=ownership ./assets "${pkgdir}${_installdir%/}/assets"
}

