# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-linapple-pie
pkgver=r54.c22ba0c
pkgrel=1
pkgdesc="Apple 2 & 2e Emulator"
arch=('x86_64')
url="https://github.com/dabonetn/linapple-pie"
license=('GPL2')
depends=(
  'curl'
  'libzip'
  'sdl_image'
)
makedepends=('git')
source=("$pkgname::git+https://github.com/dabonetn/linapple-pie")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$pkgname/src"

  make INSTDIR="${_installdir%/}"
}

package() {
  cd "$pkgname/src"

  install -Dm755 ./linapple -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ../{linapple.conf,Master.dsk,README,README-linapple-pie,CHANGELOG,INSTALL,LICENSE} -t "${pkgdir}${_installdir%/}/share"
}

