# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-vvvvvv
pkgver=2.2.r1159.70e82df
pkgrel=1
pkgdesc="2D Puzzle Game"
arch=('x86_64')
url="http://thelettervsixtim.es"
license=('custom:VVVVVV')
depends=('sdl2_mixer')
makedepends=( 
  'cmake'
  'git'
)
source=("$pkgname::git+https://github.com/TerryCavanagh/VVVVVV")
sha256sums=('SKIP')
_installdir="/opt/archrgs/ports/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "%s" "$(git describe --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  ##Default to Fullscreen
  sed -i "s/fullscreen = false/fullscreen = true/" "./desktop_version/src/Game.cpp"
}

build() {
  cd "$pkgname/build"

  cmake ../desktop_version \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}"
  make
}

package() {
  cd "$pkgname/build"

  install -Dm755 ./VVVVVV -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ../LICENSE.md "${pkgdir}${_licencedir%/}/LICENCE"
}

