# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-digger
pkgver=r264.e5c00e7
pkgrel=1
pkgdesc="Digger Digitally Remastered & Enhanced"
arch=('x86_64')
url="http://www.digger.org"
license=('GPL2')
depends=('sdl2')
makedepends=(
  'cmake'
  'git'
)
source=("$pkgname::git+https://github.com/proyvind/digger.git#branch=joystick")
sha256sums=('SKIP')
_installdir="/opt/archrgs/ports/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}"
  make
}

package() {
  cd "$pkgname/build"

  install -Dm755 ./digger -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ../{digger.txt,README.md} -t "${pkgdir}${_installdir%/}/docs"
}

