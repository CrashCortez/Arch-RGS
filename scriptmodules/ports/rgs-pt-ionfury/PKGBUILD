# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-ionfury
pkgver=r9310.b7d4ae3a5
pkgrel=1
pkgdesc="Commercial FPS Game Based On EDuke32 Source Port"
arch=('x86_64')
url="http://eduke32.com"
license=(
  'GPL2'
  'custom:Build Engine'
)
depends=(
  'alsa-lib'
  'flac'
  'gtk2'
  'libvorbis'
  'libvpx'
  'sdl2_mixer'
)
makedepends=(
  'git'
  'glu'
  'mesa'
)
source=("$pkgname::git+https://voidpoint.io/terminx/eduke32")
sha256sums=('SKIP')
_installdir="/opt/archrgs/ports/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$pkgname"

  make \
    FURY=1 \
    PREFIX="${_installdir%/}" \
    PACKAGE_REPOSITORY=1 \
    SDL_TARGET=2
}

package() {
  cd "$pkgname"

  install -Dm755 ./{fury,mapster32} -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ./package/common/buildlic.txt "${pkgdir}${_licencedir%/}/LICENCE"
}

