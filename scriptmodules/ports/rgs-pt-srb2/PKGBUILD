# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-srb2
pkgver=2.2.8
pkgrel=1
pkgdesc="3D Sonic the Hedgehog Fangame"
arch=('x86_64')
license=('GPL2')
url="http://www.srb2.org"
depends=(
  'libgme'
  'libpng'
  'sdl2_mixer'
)
makedepends=(
  'cmake'
  'git'
  'glu'
  'mesa'
)
source=(
  "$pkgname::git+https://github.com/STJr/SRB2#tag=SRB2_release_$pkgver"
  "https://github.com/STJr/SRB2/releases/download/SRB2_release_$pkgver/SRB2-v$pkgver-Full.zip"
)
sha256sums=(
  'SKIP'
  '7a3805b1127bd42db6432a9fe3e25927269e334497d48bdc9c2751edc114d718'
)
_installdir="/opt/archrgs/ports/$pkgname"

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
  mkdir -p assets/installer
  mv ../{*.dat,*.dta,*.pk3,*.txt} ./assets/installer
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DSRB2_CONFIG_HAVE_OPENMPT=OFF \
    -DSRB2_ASSET_HASHED="srb2.pk3;player.dta;music.dta;zones.pk3;patch.pk3"
  make
}

package() {
  cd "$pkgname/build"

  make DESTDIR="${pkgdir}" install
}
