# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-eduke32
pkgver=r9297.2bb6cbcae
pkgrel=1
pkgdesc="Duke Nukem 3D Port"
arch=('x86_64')
url="http://eduke32.com"
license=(
  'GPL2'
  'custom:Build Engine'
)
depends=(
  'alsa-lib'
  'flac'
  'libvorbis'
  'libvpx'
  'sdl2'
)
makedepends=(
  'git'
  'glu'
  'mesa'
)
source=("$pkgname::git+https://voidpoint.io/terminx/eduke32")
sha256sums=('SKIP')
_installdir="/opt/archrgs/ports/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$pkgname"

  make PREFIX="${_installdir%/}"
}

package() {
  cd "$pkgname"

  install -Dm755 ./{eduke32,mapster32} -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ./package/common/buildlic.txt "${pkgdir}${_licencedir%/}/LICENCE"
}
