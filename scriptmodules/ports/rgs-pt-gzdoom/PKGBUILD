# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-gzdoom
pkgver=4.6.0
pkgrel=1
_zmusicver=1.1.7
pkgdesc="Advanced DOOM Port"
arch=('x86_64')
url="http://www.zdoom.org"
license=('GPL3')
depends=(
  'alsa-lib'
  'gtk3'
  'libjpeg-turbo'
  'openal'
  'sdl2'
)
makedepends=(
  'cmake'
  'fluidsynth'
  'git'
)
provides=(
  'gzdoom=$pkgver'
  'libzmusic.so'
  'libzmusiclite.so'
)
source=(
  "$pkgname::git+https://github.com/coelckers/gzdoom#tag=g$pkgver"
  "git+https://github.com/coelckers/ZMusic#tag=$_zmusicver"
  "01_fix_file_paths.patch"
)
sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
)
_installdir="/opt/archrgs/ports/$pkgname"

prepare() {
  cd "$pkgname"

  git submodule update --init --recursive

  patch -Np1 -i ../01_fix_file_paths.patch 

  cd "${srcdir}/ZMusic/source"

  sed 's/\/sounds/\/soundfonts/g' -i ./mididevices/music_fluidsynth_mididevice.cpp

  for dir in "$pkgname" ZMusic; do
    cd "${srcdir}/$dir"
    if [[ -d ./build ]]; then
      rm -rf ./build
    fi
    mkdir ./build
  done
}

build() {
  cd "${srcdir}/ZMusic/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DDYN_FLUIDSYNTH=OFF \
    -DDYN_MPG123=OFF \
    -DDYN_SNDFILE=OFF
  make

  cd "${srcdir}/$pkgname/build"

  LDFLAGS+=" -Wl,-rpath='${_installdir%/}'"
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DDYN_GTK=OFF \
    -DDYN_OPENAL=OFF \
    -DZMUSIC_INCLUDE_DIR="${srcdir}/ZMusic/include" \
    -DZMUSIC_LIBRARIES="${srcdir}/ZMusic/build/source/libzmusic.so"
  make
}

package() {
  cd "${srcdir}/$pkgname/build"

  install -Dm755 ./gzdoom -t "${pkgdir}/${_installdir%/}"
  install -Dm644 ./*.pk3 -t "${pkgdir}/${_installdir%/}"
  install -Dm644 ../docs/{console,rh-log,skins}.* -t "${pkgdir}/${_installdir%/}/docs"
  install -Dm644 ./soundfonts/gzdoom.sf2 -t "${pkgdir}/${_installdir%/}/soundfonts"
  install -Dm644 ./fm_banks/{*.wopl,*.wopn} -t "${pkgdir}/${_installdir%/}/fm_banks"

  cd "${srcdir}/ZMusic/build"

  install -Dm644 ./source/libzmusic* -t "${pkgdir}/${_installdir%/}"
}

