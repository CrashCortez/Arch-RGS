# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-gzdoom
pkgver=4.5.0
pkgrel=1
pkgdesc="Advanced DOOM Port"
arch=('x86_64')
url="http://www.zdoom.org"
license=('GPL3')
depends=(
  'alsa-lib'
  'libjpeg-turbo'
  'openal'
  'sdl2'
)
makedepends=(
  'cmake'
  'desktop-file-utils'
  'fluidsynth'
  'git'
  'gtk3'
)
provides=('gzdoom=$pkgver' 'libzmusic.so' 'libzmusiclite.so')
source=(
  "$pkgname::git+https://github.com/coelckers/gzdoom#tag=g$pkgver"
  "git+https://github.com/coelckers/ZMusic#tag=1.1.3"
)
sha256sums=(
  'SKIP'
  'SKIP'
)
_installdir="/opt/archrgs/ports/$pkgname"

prepare() {
  cd "$pkgname"

  git submodule update --init --recursive

  cd ${srcdir}/ZMusic/source

  sed 's/\/sounds/\/soundfonts/g' -i ./mididevices/music_fluidsynth_mididevice.cpp

  for dir in $pkgname ZMusic; do
    cd ${srcdir}/$dir
    if [[ -d build ]]; then
      rm -rf build
    fi
    mkdir build
  done
}

build() {
  cd "${srcdir}/ZMusic/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "${srcdir}/$pkgname/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DNO_FMOD=OFF \
    -DZMUSIC_INCLUDE_DIR="${srcdir}/ZMusic/include" \
    -DZMUSIC_LIBRARIES="${srcdir}/ZMusic/build/source/libzmusic.so"
  make
}

package() {
  cd "${srcdir}/ZMusic/build"

  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/$pkgname/build"

  make DESTDIR="${pkgdir}" install

  install -Dm644 ./soundfonts/*.sf2 -t "${pkgdir}${_installdir%/}/share/soundfonts"
  install -Dm644 ./fm_banks/{*.wopl,*.wopn} -t "${pkgdir}${_installdir%/}/share/fm_banks"
}

