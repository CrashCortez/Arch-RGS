# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-alephone
pkgver=1.4
_release=release-20210408
pkgrel=1
pkgdesc="Marathon 2 Game Engine"
arch=('x86_64')
url="https://alephone.lhowon.org"
license=('GPL3')
depends=(
  'boost-libs'
  'ffmpeg'
  'glu'
  'libmad'
  'libvorbis'
  'sdl2_image'
  'sdl2_net'
  'sdl2_ttf'
  'zziplib'
)
makedepends=(
  'boost'
  'icoutils'
  'mesa'
)
source=("https://github.com/Aleph-One-Marathon/alephone/archive/refs/tags/$_release.tar.gz")
sha256sums=('SKIP')
_installdir="/opt/archrgs/ports/$pkgname"

build() {
  cd "alephone-$_release"

  ./autogen.sh
  ./configure \
    --prefix="${_installdir%/}" \
    --bindir="${_installdir%/}" \
    --libdir="${_installdir%/}" \
    --datarootdir="${_installdir%/}"
  make
}

package() {
  cd "alephone-$_release"

  make PREFIX="${_installdir%/}" DESTDIR="${pkgdir}" install
}

