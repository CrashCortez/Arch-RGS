# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-srb2kart
pkgver=1.3
pkgrel=1
pkgdesc="Kart Racing Mod Based On Sonic Robo Blast 2"
arch=('x86_64')
license=('GPL2')
url="https://mb.srb2.org/showthread.php?t=43708"
depends=(
  'libgme'
  'libpng'
  'sdl2_mixer'
)
makedepends=(
  'cmake'
  'git'
  'glu'
  'mesa'
)
source=(
  "$pkgname::git+https://github.com/STJr/Kart-Public#tag=v$pkgver"
  "https://github.com/STJr/Kart-Public/releases/download/v$pkgver/srb2kart-v${pkgver//./}-Installer.exe"
)
sha256sums=(
  'SKIP'
  '9a198781cc1f1df2b0a3b6dcf6677c0a8663d0dc182d20cd6bae1bc78f37632e'
)
_installdir="/opt/archrgs/ports/$pkgname"

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
  mkdir -p assets/installer

  mv ../{*.kart,*.srb} ./assets/installer
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}"
  make
}

package() {
  cd "$pkgname/build"

  make DESTDIR="${pkgdir}" install
}
