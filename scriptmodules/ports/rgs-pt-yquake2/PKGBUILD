# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-yquake2
pkgver=7.45.r163.g2fae58d5
pkgrel=1
pkgdesc="Yamagi Quake II Client Including Ground Zero, The Reckoning & Capture The Flag"
url="http://www.yamagi.org/quake2"
arch=('x86_64')
license=('GPL2')
depends=('sdl2')
makedepends=(
  'cmake'
  'git'
)
source=(
  "$pkgname::git+https://github.com/yquake2/yquake2"
  "$pkgname-rogue::git+https://github.com/yquake2/rogue"
  "$pkgname-xatrix::git+https://github.com/yquake2/xatrix"
  "$pkgname-ctf::git+https://github.com/yquake2/ctf"
)
sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
)
_installdir="/opt/archrgs/ports/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "%s" "$(git describe --tags | sed 's/^QUAKE2_//;s/_/./;s/\([^-]*-g\)/r\1/;s/-/./g')"
}

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DSYSTEMWIDE_SUPPORT=OFF
  make

  for dir in "$pkgname-rogue" "$pkgname-xatrix" "$pkgname-ctf"; do
    cd "$srcdir/$dir"
    make
  done
}

package() {
  cd "$pkgname/build"

  install -Dm755 ./release/{quake2,q2ded,*.so} -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ./release/baseq2/game.so -t "${pkgdir}${_installdir%/}/bin/baseq2"
  install -Dm644 ../stuff/yq2.cfg -t "${pkgdir}${_installdir%/}/share/doc"
  install -Dm644 ../doc/*.md -t "${pkgdir}${_installdir%/}/share/doc"
  install -Dm644 ../stuff/icon/Quake2.* -t "${pkgdir}${_installdir%/}/share/pixmaps"

  cd "$srcdir/$pkgname-rogue"
  install -Dm644 ./release/game.so -t "${pkgdir}${_installdir%/}/bin/rogue"

  cd "$srcdir/$pkgname-xatrix"
  install -Dm644 ./release/game.so -t "${pkgdir}${_installdir%/}/bin/xatrix"

  cd "$srcdir/$pkgname-ctf"
  install -Dm644 ./release/game.so -t "${pkgdir}${_installdir%/}/bin/ctf"
}

