# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-abuse
pkgver=r50.42b6acd
pkgrel=1
pkgdesc="Abuse SDL Port"
arch=('x86_64')
url="https://github.com/Xenoveritas/abuse"
license=('custom:Abuse' 'custom:WTFPL')
depends=('sdl2_mixer')
makedepends=(
  'cmake'
  'git'
)
source=(
  "$pkgname::git+https://github.com/Xenoveritas/abuse"
  "https://gitlab.com/arch-rgs/arch-rgs-resources/-/raw/assets/rgs-pt-abuse/abuse_assets.tar.zst"
)
sha256sums=(
  'SKIP'
  '73fc7261b1748ce23936b65e114011a4d88729b557e9b9125c608119ec0118ac'
)
_installdir="/opt/archrgs/ports/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  sed -e "s|ASSETDIR \"share/games/abuse\"|ASSETDIR "data"|g" -i ./CMakeLists.txt

  cd "$srcdir/assets"

  tar --zstd -xf ./abuse_assets.tar.zst && \
  cp -r ./abuse_assets/data $srcdir/$pkgname
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}"
  make
}

package() {
  cd "$pkgname/build"

  make DESTDIR="${pkgdir}" install

  install -Dm644 ../COPYING "${pkgdir}${_licencedir%/}/LICENCE"
  install -Dm644 ../COPYING.WTFPL "${pkgdir}${_licencedir%/}/LICENCE.WTFPL"
}

