# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-uhexen2
pkgver=1.5.9
pkgrel=1
pkgdesc="Hexen II Source Port"
arch=('x86_64')
url="http://uhexen2.sourceforge.net"
license=('GPL2')
depends=(
  'libmad' 
  'libvorbis' 
  'sdl'
)
makedepends=(
  'nasm'
)
source=(
  "http://downloads.sourceforge.net/uhexen2/hexen2source-$pkgver.tgz"
  "01_Fix_RELOC_Missing.patch"
)
sha256sums=(
  '2aa84c141a820f9087850aacf3684a5f71c434428bc57545899eda1b9a28c3e0'
  '8320af850d4e30d64d3ebb61ae344f2070727a9fbefc13ab931b6df486d6120c'
)
_installdir="/opt/archrgs/ports/$pkgname"

prepare() {
  cd "hexen2source-$pkgver/engine/hexen2"

  patch -Np2 -i $srcdir/01_Fix_RELOC_Missing.patch
}

build() {
  cd "hexen2source-$pkgver/engine/hexen2"

  ./build_all.sh
}

package() {
  cd "hexen2source-$pkgver/engine/hexen2"

  install -Dm755 ./{hexen2,glhexen2} -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ../../docs/* -t "${pkgdir}${_installdir%/}/share/doc"
}

