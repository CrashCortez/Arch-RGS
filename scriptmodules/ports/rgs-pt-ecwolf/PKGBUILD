# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-ecwolf
pkgver=1.3.3.r366.g1f5f341
pkgrel=1
pkgdesc="Advanced Source Port For Wolfenstein 3D, Spear Of Destiny And Super 3D Noah's Ark"
arch=('x86_64')
url="http://maniacsvault.net/ecwolf"
license=(
  'custom:id'
  'custom:MAME'
)
depends=(
  'flac'
  'fluidsynth'
  'gtk3'
  'libjpeg'
  'libmodplug'
  'libvorbis'
  'opusfile'
  'sdl2_mixer'
  'sdl2_net'
)
makedepends=(
  'git'
  'cmake'
)
source=("$pkgname::git+https://bitbucket.org/ecwolf/ecwolf")
sha256sums=('SKIP')
_installdir="/opt/archrgs/ports/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "%s" "$(git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g')"
}

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd "$pkgname/build"

  LDFLAGS="-lvorbisfile -lopusfile -lFLAC  -lmodplug -lfluidsynth $LDFLAGS"
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DGPL=OFF
  make
}

package() {
  cd "$pkgname/build"

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}${_installdir%/}/games" "${pkgdir}${_installdir%/}/bin"

  cd ../docs
  for licence in ./license-{id,mame}.txt; do
    install -Dm644 "$licence" "${pkgdir}${_licencedir%/}/$licence"
  done
}

