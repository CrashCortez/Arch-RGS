# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-supertuxkart
pkgver=1.2
pkgrel=1
pkgdesc="3D Kart Racing Game Featuring Tux & Friends"
arch=('x86_64')
url="https://supertuxkart.net"
license=('GPL3')
depends=(
  'bluez-libs'
  'glew'
  'libjpeg-turbo'
  'libvorbis'
  'libvpx'
  'openal'
  'python'
  'sdl2'
  'sqlite'
)
makedepends=(
  'cmake'
  'mesa'
)
source=(
  "https://github.com/supertuxkart/stk-code/releases/download/$pkgver/SuperTuxKart-$pkgver-src.tar.xz"
  "01_fix_sdl_compilation_error.patch"
)
sha256sums=(
  '052edf0afdbeb99583fe8676fb0ab80ecb6103fb88b7540f858d1b5fa1297d37'
  '7532747eb43642790117ff611ca93147e3b67a15663a094160aecc331b59ea11'
)
_installdir="/opt/archrgs/ports/$pkgname"

prepare() {
  cd SuperTuxKart-$pkgver-src

  patch -Nup2 -i ../01_fix_sdl_compilation_error.patch

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd SuperTuxKart-$pkgver-src/build

  cmake .. \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_RECORDER=0
  ninja
}

package() {
  cd SuperTuxKart-$pkgver-src/build

  DESTDIR="${pkgdir}" ninja install
}
