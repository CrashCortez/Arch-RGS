# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-wolf4sdl
pkgver=r10.5387b99
pkgrel=1
pkgdesc="Port of Wolfenstein 3D & Spear of Destiny"
arch=('x86_64')
url="https://github.com/mozzwald/wolf4sdl"
license=('custom:MAME' 'custom:id')
depends=('sdl_mixer')
makedepends=('git')
source=("$pkgname::git+https://github.com/mozzwald/wolf4sdl")
sha256sums=('SKIP')
_installdir="/opt/archrgs/ports/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"
_get_opts_wolf4sdl=(
  'wolf4sdl-sw-v14 -DCARMACIZED -DUPLOAD'
  'wolf4sdl-3dr-v14 -DCARMACIZED'
  'wolf4sdl-gt-v14 -DCARMACIZED -DGOODTIMES'
  'wolf4sdl-spear -DCARMACIZED -DGOODTIMES -DSPEAR'
  'wolf4sdl-spear-sw -DCARMACIZED -DSPEARDEMO -DSPEAR'
)

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"

  if [[ -d bin ]]; then
    rm -rf bin
  fi
  mkdir bin
}

build() {
  cd "$pkgname"

  local bin
  local defs
  local opt

  for opt in "${_get_opts_wolf4sdl[@]}"; do
    bin="${opt%% *}"
    defs="${opt#* }"
    CFLAGS+=" -DVERSIONALREADYCHOSEN $defs" \
    make PREFIX="${_installdir%/}"
    mv ./wolf3d "bin/$bin"
  done
}

package() {
  cd "$pkgname"

  install -Dm755 ./bin/* -t "${pkgdir}${_installdir%/}/bin"
  install -Dm644 ./man6/* -t "${pkgdir}${_installdir%/}/share/man/man6"

  for licence in ./license-{id,mame}.txt; do
    install -Dm644 "$licence" "${pkgdir}${_licencedir%/}/$licence"
  done
}

