# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-opensurge
pkgver=0.5.2
_scriptver=0.5.5
pkgrel=1
pkgdesc="Fun 2D Retro Platformer & Game Engine"
arch=('x86_64')
url="https://github.com/alemart/opensurge"
license=('GPL3')
depends=('allegro')
makedepends=(
  'cmake'
  'git'
)
source=(
  "$pkgname::git+https://github.com/alemart/opensurge#tag=v$pkgver"
  "git+https://github.com/alemart/surgescript#tag=v$_scriptver"
)
sha256sums=(
  'SKIP'
  'SKIP'
)
OPTIONS=(!emptydirs)
_installdir="/opt/archrgs/ports/$pkgname"

prepare() {
  cd "$pkgname"

  if [[ -d ./build ]]; then
    rm -rf ./build
  fi
  mkdir ./build
}

build() {
  cd surgescript

  cmake . \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DICON_PATH="${_installdir%/}/share/pixmaps" \
    -DMETAINFO_PATH="${_installdir%/}/share/metainfo" \
    -DWANT_SHARED=ON \
    -DWANT_STATIC=OFF
  make

  cd "$srcdir/$pkgname/build"

  LDFLAGS+=" -Wl,-rpath='${_installdir%/}/lib'" \
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DGAME_BINDIR="${_installdir%/}/bin" \
    -DGAME_DATADIR="${_installdir%/}/share/$pkgname" \
    -DDESKTOP_INSTALL="OFF" \
    -DSURGESCRIPT_INCLUDE_PATH="$srcdir/surgescript/src" \
    -DSURGESCRIPT_LIBRARY_PATH="$srcdir/surgescript"
  make
}

package() {
  cd "$pkgname/build"

  make DESTDIR="${pkgdir}" install

  cd "$srcdir/surgescript"

  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}/usr"
}

