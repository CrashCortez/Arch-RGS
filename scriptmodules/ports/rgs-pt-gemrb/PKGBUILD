# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-gemrb
pkgver=0.8.7.r351.7ae2fe110
pkgrel=1
pkgdesc="Port of Bioware's Infinity Engine"
arch=('x86_64')
url="http://www.gemrb.org"
license=('GPL2')
depends=(
  'glew'
  'libvorbis'
  'openal'
  'python2'
  'vlc'
)
makedepends=(
  'cmake'
  'git'
)
source=("$pkgname::git+https://github.com/gemrb/gemrb")
sha256sums=('SKIP')
_installdir="/opt/archrgs/ports/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "%s" "$(git describe --long | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DFREETYPE_INCLUDE_DIRS=/usr/include/freetype2/ \
    -DDISABLE_WERROR=1 \
    -DOPENGL_BACKEND=OpenGL \
    -DSDL_BACKEND=SDL2 \
    -DUSE_SDLMIXER=OFF
  make
}

package() {
  cd "$pkgname/build"

  make DESTDIR="${pkgdir}" install
}
