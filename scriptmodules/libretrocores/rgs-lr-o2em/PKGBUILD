# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-o2em
pkgver=140
pkgrel=1
pkgdesc="Libretro Core - Magnavox Odyssey 2 (Philips VideoPac)"
arch=('x86_64')
url="https://github.com/libretro/libretro-o2em"
license=('custom:Artistic License 2.0')
depends=('glibc')
makedepends=('git')
source=("$pkgname::git+https://github.com/libretro/libretro-o2em")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

build() {
  cd "$pkgname"

  make
}

package() {
  cd "$pkgname"

  install -Dm644 ./o2em_libretro.so -t "${pkgdir}${_installdir%/}"
  install -Dm644 ./COPYING "${pkgdir}${_licencedir%/}/LICENCE"
}

