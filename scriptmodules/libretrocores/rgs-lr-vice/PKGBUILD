# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-vice
pkgver=20657
pkgrel=1
pkgdesc="Libretro Core - Commodore C64 & VIC 20"
arch=('x86_64')
url="https://github.com/libretro/vice-libretro"
license=('GPL2')
depends=('gcc-libs')
makedepends=('git')
source=("$pkgname::git+https://github.com/libretro/vice-libretro")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

build() {
  cd "$pkgname"

  for emutype in x64 x64dtv x64sc x128 xpet xplus4 xvic; do
    make clean
    make EMUTYPE="$emutype"
  done
}

package() {
  cd "$pkgname"

  for emutype in x64 x64dtv x64sc x128 xpet xplus4 xvic; do
    install -Dm644 ./vice_"$emutype"_libretro.so -t "${pkgdir}${_installdir%/}"
  done
}

