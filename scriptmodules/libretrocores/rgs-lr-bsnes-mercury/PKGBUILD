# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-bsnes-mercury
pkgver=324
pkgrel=1
pkgdesc="Libretro Core - Super Nintendo Entertainment System"
arch=('x86_64')
url="https://github.com/libretro/bsnes-mercury"
license=('GPL3')
depends=('gcc-libs')
makedepends=('git')
source=("$pkgname::git+https://github.com/libretro/bsnes-mercury")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"

prepare() {
  cd "$pkgname"

  ##SURPRESS UNDEFINED REFERENCE ERRORS WHEN BUILDING THE BALANCED CORE
  sed -e "s|--no-undefined -Wl,||g" -i ./Makefile
}

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

build() {
  cd "$pkgname"

  for profile in 'accuracy' 'balanced' 'performance'; do
    make clean
    make PROFILE="$profile"
  done
}

package() {
  cd "$pkgname"

  for profile in 'accuracy' 'balanced' 'performance'; do
    install -Dm644 ./bsnes_mercury_"$profile"_libretro.so -t "${pkgdir}${_installdir%/}"
  done
}
