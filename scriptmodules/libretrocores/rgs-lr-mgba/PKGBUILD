# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-mgba
pkgver=6837
pkgrel=1
pkgdesc="Libretro Core - Nintendo Game Boy, Game Boy Advance & Game Boy Color"
arch=('x86_64')
url="https://github.com/libretro/mgba"
license=('custom:MPL2')
depends=('glibc')
makedepends=('git')
source=("$pkgname::git+https://github.com/libretro/mgba")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

build() {
  cd "$pkgname"

  make -f Makefile.libretro
}

package() {
  cd "$pkgname"

  install -Dm644 ./mgba_libretro.so -t "${pkgdir}${_installdir%/}"
  install -Dm644 ./LICENSE "${pkgdir}${_licencedir%/}/LICENCE"
}

