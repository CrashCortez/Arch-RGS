# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-craft
pkgver=1059
pkgrel=1
pkgdesc="Libretro Core - Craft (Minecraft Clone)"
arch=('x86_64')
url="https://www.michaelfogleman.com/craft"
license=('MIT')
depends=('libglvnd')
makedepends=('git')
source=("$pkgname::git+https://github.com/libretro/craft")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

build() {
  cd "$pkgname"

  make -f Makefile.libretro
}

package() {
  cd "$pkgname"

  install -Dm644 ./craft_libretro.so -t "${pkgdir}${_installdir%/}"
  install -Dm644 ./LICENSE.md "${pkgdir}${_licencedir%/}/LICENCE"
}

