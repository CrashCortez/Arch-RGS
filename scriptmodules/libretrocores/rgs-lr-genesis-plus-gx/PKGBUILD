# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-genesis-plus-gx
pkgver=1589
pkgrel=1
pkgdesc="Libretro Core - Sega Master System, Game Gear, Mega Drive (Genesis), Sega CD & SG-1000"
arch=('x86_64')
url="https://github.com/libretro/Genesis-Plus-GX"
license=('BSD' '(L)GPL2')
depends=('glibc')
makedepends=('git')
source=("$pkgname::git+https://github.com/libretro/Genesis-Plus-GX")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

build() {
  cd "$pkgname"

  make -f Makefile.libretro
}

package() {
  cd "$pkgname"

  install -Dm644 ./genesis_plus_gx_libretro.so -t "${pkgdir}${_installdir%/}"
  install -Dm644 ./LICENSE.txt "${pkgdir}${_licencedir%/}/LICENCE"
}

