# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-openlara
pkgver=1059
pkgrel=1
arch=('x86_64')
pkgdesc="Libretro Core - Tomb Raider"
url="https://github.com/libretro/OpenLara"
license=('BSD')
depends=(
  'libglvnd'
  'libpulse'
)
makedepends=(
  'gcc9'
  'git'
)
source=("$pkgname::git+https://github.com/libretro/OpenLara")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

build() {
  cd "$pkgname/src/platform/libretro"

  make CC=gcc-9
}

package() {
  cd "$pkgname/src/platform/libretro"

  install -Dm644 ./openlara_libretro.so -t "${pkgdir}${_installdir%/}"
  install -Dm644 "$srcdir/$pkgname/LICENSE" "${pkgdir}${_licencedir%/}/LICENCE"
  install -dm644 "${pkgdir}${_installdir%/}/data"
  cp -r "$srcdir/$pkgname"/bin/{audio,level,*.json} "${pkgdir}${_installdir%/}/data"
}

