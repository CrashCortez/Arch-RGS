# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-ppsspp
pkgver=26710
pkgrel=1
pkgdesc="Libretro Core - Sony PlayStation Portable"
arch=('x86_64')
url="https://github.com/libretro/ppsspp"
license=('GPL2')
depends=('glew')
makedepends=(
  'cmake'
  'git'
  'mesa'
)
source=("$pkgname::git+https://github.com/libretro/ppsspp")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

prepare() {
  cd "$pkgname"

  git submodule update --init --recursive

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DLIBRETRO=ON
  make
}

package() {
  cd "$pkgname/build"

  install -Dm644 ./lib/ppsspp_libretro.so -t "${pkgdir}${_installdir%/}"
  cp -r ./assets "${pkgdir}${_installdir%/}"
}
