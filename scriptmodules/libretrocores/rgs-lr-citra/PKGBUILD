# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-citra
pkgver=6968
pkgrel=1
pkgdesc="Libretro Core - Nintendo 3DS"
arch=('x86_64')
url="https://github.com/libretro/citra"
license=('GPL2')
depends=('sdl2')
makedepends=(
  'cmake' 
  'git'
)
source=("$pkgname::git+https://github.com/libretro/citra")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

prepare() {
  cd "$pkgname"

  git submodule update --init --recursive

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_LIBRETRO=ON \
    -DENABLE_QT=OFF \
    -DENABLE_SDL2=ON \
    -DENABLE_WEB_SERVICE=OFF
  make
}

package() {
  cd "$pkgname/build"

  install -Dm644 ./src/citra_libretro/citra_libretro.so -t "${pkgdir}${_installdir%/}"
}
