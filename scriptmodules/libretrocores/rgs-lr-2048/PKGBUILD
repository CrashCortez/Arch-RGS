# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-2048
pkgver=166
pkgrel=1
pkgdesc="Libretro Core - 2048"
arch=('x86_64')
url="https://github.com/libretro/libretro-2048"
license=('custom:The Unlicense')
depends=('glibc')
makedepends=('git')
source=("$pkgname::git+https://github.com/libretro/libretro-2048")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

build() {
  cd "$pkgname"

  make -f Makefile.libretro
}

package() {
  cd "$pkgname"

  install -Dm644 ./2048_libretro.so -t "${pkgdir}${_installdir%/}"
  install -Dm644 ./COPYING "${pkgdir}${_licencedir%/}/LICENCE"
}

