# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-fe-emulationstation
pkgver=2.7.6.r424.be9e0f32
pkgrel=1
pkgdesc="Flexible Emulator Front-End"
arch=('x86_64')
url="https://github.com/RetroPie/EmulationStation"
license=('MIT')
depends=(
  'alsa-lib'
  'boost-libs'
  'freeimage'
  'glu'
  'libcec'
  'sdl2'
  'ttf-dejavu'
  'vlc'
)
makedepends=(
  'boost'
  'cmake'
  'eigen'
  'freetype2'
  'git'
  'libsm'
  'mesa'
  'pugixml'
  'rapidjson'
)
source=("$pkgname::git+https://github.com/retropie/emulationStation.git#tag=v2.9.4")
sha256sums=('SKIP')
_installdir="/opt/archrgs/frontends/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "%s" "$(git describe --long | sed 's/^v//; s/\([^-]*-\)g/r\1/; s/-/./g')"
}

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  git submodule update --init --recursive
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX="${_installdir%/}" 
  make
}

package() {
  cd "$pkgname"

  install -Dm755 ./{emulationstation,emulationstation.sh,CREDITS.md,GAMELISTS.md,README.md,THEMES.md} -t "${pkgdir}${_installdir%/}"
  install -Dm644 ./LICENSE.md "${pkgdir}${_licencedir%/}/LICENSE"
  cp -r ./resources "${pkgdir}${_installdir%/}"
}

