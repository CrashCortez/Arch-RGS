# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-sr-skyscraper
pkgver=3.6.14
pkgrel=1
pkgdesc="Powerful & Versatile Game Scraper"
arch=("x86_64")
url="https://github.com/muldjord/skyscraper"
license=('GPL3')
depends=('qt5-base')
makedepends=('git')
source=("$pkgname::git+https://github.com/muldjord/skyscraper#tag=$pkgver")
sha256sums=('SKIP')
_installdir="/opt/archrgs/frontends/$pkgname"

prepare() {
  cd "$pkgname"

  sed -i "s_/usr/local/etc_${_installdir%/}/share_g" ./skyscraper.pro
  sed -i "s_/usr/local/bin_${_installdir%/}/bin_g" ./skyscraper.pro
}

build() {
  cd "$pkgname"

  qmake
  make
}

package() {
  cd "$pkgname"

  make INSTALL_ROOT="${pkgdir}" install
}

