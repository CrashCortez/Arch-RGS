# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-lr-gw
pkgver=266
pkgrel=1
pkgdesc="Libretro Nintendo Game & Watch Core"
arch=('x86_64')
url="https://github.com/libretro/gw-libretro"
license=('custom:zlib')
depends=(
  'glibc'
  'zlib'
)
makedepends=('git')
source=("$pkgname::git+https://github.com/libretro/gw-libretro")
sha256sums=('SKIP')
_installdir="/opt/archrgs/libretrocores/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  git rev-list --count HEAD
}

build() {
  cd "$pkgname"

  make
}

package() {
  cd "$pkgname"

  install -Dm644 ./gw_libretro.so -t "${pkgdir}${_installdir%/}"
  install -Dm644 ./LICENSE -t "${pkgdir}${_licencedir%/}"
}
