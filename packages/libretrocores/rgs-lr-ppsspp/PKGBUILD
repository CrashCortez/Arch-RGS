# Maintainer: V0rt3x667 <al-g0l@outlook.com>

pkgname=rgs-lr-ppsspp
pkgver=25774
pkgrel=1
pkgdesc="Libretro implementation of PPSSPP. (Sony PlayStation Portable)"
arch=('x86_64')
url='https://github.com/libretro/ppsspp'
license=('GPL2')
depends=('libgl' 'sdl2' 'zlib')
makedepends=('cmake' 'git' 'mesa')
source=("$pkgname::git+https://github.com/libretro/ppsspp.git"
        'git+https://github.com/Kingcom/armips.git'
        'git+https://github.com/discordapp/discord-rpc.git'
        'git+https://github.com/hrydgard/ppsspp-ffmpeg.git'
        'ppsspp-glslang::git+https://github.com/hrydgard/glslang.git'
        'git+https://github.com/hrydgard/ppsspp-lang.git'
        'git+https://github.com/Tencent/rapidjson.git'
        'git+https://github.com/KhronosGroup/SPIRV-Cross.git'
        'armips-tinyformat::git+https://github.com/Kingcom/tinyformat.git')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname

  git rev-list --count HEAD
}

prepare() {
  cd $pkgname

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  for submodule in ffmpeg assets/lang ext/glslang; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../ppsspp-${submodule#*/}
    git submodule update ${submodule}
  done

  for submodule in ext/{armips,discord-rpc,rapidjson,SPIRV-Cross}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git submodule update ${submodule}
  done

  cd ext/armips

  for submodule in ext/tinyformat; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../../../armips-${submodule#*/}
    git submodule update ${submodule}
  done
}

build() {
  cd $pkgname/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_SKIP_RPATH='ON' \
    -DLIBRETRO='ON'
  make -j$(nproc)
}

package() {
  cd $pkgname/build

  install -Dm644 ./lib/ppsspp_libretro.so -t "${pkgdir}/opt/archrgs/libretrocores/$pkgname/"
  cp -r ./assets -t "${pkgdir}/opt/archrgs/libretrocores/$pkgname/"
}
