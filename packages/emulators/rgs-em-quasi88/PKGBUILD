# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-quasi88
pkgver=0.6.4
pkgrel=1
pkgdesc="An NEC PC-8801 Emulator"
arch=('x86_64')
url="https://www.eonet.ne.jp/~showtime/quasi88"
license=('GPL')
depends=(
  'gcc-libs'
  'sdl'
)
source=(
  "https://www.eonet.ne.jp/~showtime/quasi88/release/quasi88-$pkgver.tgz"
  "01_Fixes.patch"
)
sha256sums=(
  '2c4329f9f77e02a1e1f23c941be07fbe6e4a32353b5d012531f53b06996881ff'
  '8f640ac90cccbfea60b4b1324044e7ecbdf52866fd94e4875578b236b6176b4c'
)
_installdir="/opt/archrgs/emulators/$pkgname"

prepare() {
  cd "quasi88-$pkgver"
  
  patch -p1 -i ../01_Fixes.patch
}

build() {
  cd "quasi88-$pkgver"
  
  make \
    X11_VERSION=1 \
    SDL_VERSION=1 \
    ARCH=linux \
    SOUND_SDL=1 \
    USE_OLD_MAME_SOUND=1 \
    USE_FMGEN=1
}

package() {
  cd "quasi88-$pkgver"
  
  mkdir -p "$pkgdir/$_installdir/bin"
  
  make DESTDIR="$pkgdir" BINDIR="$pkgdir/$_installdir/bin/" install
}
