# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-mame
pkgver=0.221
pkgrel=1
pkgdesc="A Multiple Arcade Machine Emulator"
url="https://mamedev.org/"
license=('GPL2')
arch=('x86_64')
depends=(
  'libutf8proc'
  'lua' 
  'portaudio'
  'portmidi' 
  'qt5-base'
  'pugixml'
  'sdl2'
  'sdl2_ttf'
)
makedepends=(
  'asio'
  'glm'
  'libxinerama'
  'nasm' 
  'python' 
  'rapidjson' 
)
_latestver=$(wget -qO- https://api.github.com/repos/mamedev/mame/releases/latest | grep -m 1 tag_name | cut -d\" -f4)
source=("$pkgname::git+https://github.com/mamedev/mame#tag=${_latestver}")
sha256sums=('SKIP')
_installdir="/opt/archrgs/emulators/$pkgname"

pkgver() {
  cd "$pkgname"
  
  printf "%s" "$_latestver"
}

prepare() {
  cd "$pkgname" || exit
    
  #Use System Libraries
  sed -e 's|\# USE_SYSTEM_LIB|USE_SYSTEM_LIB|g' -i makefile
  #Except for ASIO
  sed -e 's|USE_SYSTEM_LIB_ASIO|\# USE_SYSTEM_LIB_ASIO|g' -i makefile
}

build() {
  cd "$pkgname" || exit
    
  make \
    PREFIX="$_installdir" \
    BUILDDIR=./build \
    ARCHOPTS=-flifetime-dse=1 \
    NOWERROR=1 \
    TOOLS=1
}

package() {
  cd "$pkgname" || exit

  _BIN=(
    'castool' 
    'chdman' 
    'imgtool' 
    'jedutil' 
    'nltool' 
    'nlwav' 
    'pngcmp' 
    'regrep' 
    'romcmp'
    'split' 
    'srcclean' 
    'ldverify' 
    'ldresample' 
    'testkeys' 
    'unidasm'
)

  #Install Binaries
  install -Dm755 ./mame64 "$pkgdir/$_installdir/bin/mame"
  
  for bin in "${_BIN[@]}"; do
    install -Dm755 "$bin" "$pkgdir/$_installdir/bin"
  done

  #Install Data
  cp -ar ./{artwork,bgfx,docs,plugins,language,ctrlr,keymaps,hash} "${pkgdir}${_installdir%/}"
}
