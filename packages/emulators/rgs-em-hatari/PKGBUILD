# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-em-hatari
pkgver=2.2.1
pkgrel=1
pkgdesc="Atari ST, STE, TT & Falcon Emulator"
arch=('x86_64')
url='http://hatari.sourceforge.net'
license=('GPL')
depends=(
  'libpng' 
  'portaudio' 
  'portmidi' 
  'python' 
  'systemd-libs'
)
makedepends=(
  'cmake' 
  'systemd'
)
source=(
  "https://download.tuxfamily.org/hatari/$pkgver/hatari-$pkgver.tar.bz2"
  "http://www.kryoflux.com/download/spsdeclib_5.1_source.zip"
)
sha256sums=(
  '11afef30a274be84696588583d5a1d65c8046934670f718c311c956ef7106f60'
  'e60b756fa68c5fe073a28c618a0ec06220ef185e2a3c59e4a8d001c930f693d1'
)
_installdir="/opt/archrgs/emulators/$pkgname"

prepare() {
  unzip -o ./capsimg_source_linux_macosx.zip
  chmod u+x ./capsimg_source_linux_macosx/CAPSImg/configure
  
  cd "hatari-$pkgver" || exit
  
  #Use CAPSImage 5
  sed -i "s/CAPSIMAGE_VERSION 4/CAPSIMAGE_VERSION 5/" ./cmake/FindCapsImage.cmake
  
  #CAPSImage 5.1 Add Missing Types
  sed -i "s/CapsLong/Sint32/g" ./src/floppy_ipf.c
  sed -i "s/CapsULong/Uint32/g" ./src/floppy_ipf.c
}

build() {
  cd "capsimg_source_linux_macosx/CAPSImg" || exit
  
  #Build CAPSImage
  ./configure
  make
  
  #Copy CAPSImg Header Files
  install -Dm644 ../LibIPF/*.h -t "$srcdir/hatari-$pkgver/src/includes/caps5/"
  install -Dm644 ../Core/CommonTypes.h -t "$srcdir/hatari-$pkgver/src/includes/caps5/"

  cd "$srcdir/hatari-$pkgver" || exit
  
  rm -f CMakeCache.txt
  #Remove __cdecl
  CFLAGS+=" -D__cdecl=''" LDFLAGS+=" -Wl,-rpath='$_installdir/bin/'" \
  cmake . \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX:PATH="$_installdir" \
    -DCAPSIMAGE_INCLUDE_DIR="$srcdir/hatari-$pkgver/src/includes/caps5/" \
    -DCAPSIMAGE_LIBRARY="$srcdir/capsimg_source_linux_macosx/CAPSImg/libcapsimage.so.5.1" \
    -DENABLE_SDL2:BOOL=1
  make
}

package() {
  cd "hatari-$pkgver" || exit

  make DESTDIR="$pkgdir" install
  
  install -Dm644 "$srcdir/capsimg_source_linux_macosx/CAPSImg/libcapsimage.so.5.1" \
    -t "${pkgdir}${_installdir%/}/bin/"
  
  ln -s "$_installdir/bin/libcapsimage.so.5.1" "${pkgdir}${_installdir%/}/bin/libcapsimage.so.5"
}
