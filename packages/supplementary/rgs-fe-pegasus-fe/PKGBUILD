# Maintainer: V0rt3x667 <al-g0l at outlook dot com>
pkgname=rgs-fe-pegasus-fe
pkgver=r1550.fdb474a
pkgrel=1
pkgdesc="A Graphical Front-End for Launching Emulators"
arch=('x86_64')
url="http://pegasus-frontend.org"
license=('GPL3')
depends=(
  'qt-gstreamer'
  'fontconfig'
  'openssl-1.0'
  'polkit'
)
makedepends=(
  'qt5-tools'
  'qt5-quickcontrols'
  'qt5-gamepad'
  'qt5-svg'
  'qt5-multimedia'
  'qt5-graphicaleffects'
  'qt5-declarative'
  'qt5-imageformats'
  'sdl2'
)
source=("$pkgname::git+https://github.com/mmatyas/pegasus-frontend")
sha256sums=('SKIP')
_installdir="/opt/archrgs/supplementary/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"

  git submodule update --init
  git submodule update --init --recursive

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd "$pkgname/build"

  qmake .. \
    USE_SDL_GAMEPAD='1' \
    INSTALL_BINDIR="$_installdir/bin" \
    INSTALL_DOCDIR="$_installdir/share/doc/$pkgname" \
    INSTALL_ICONDIR="$_installdir/share/pixmaps" \
    INSTALL_DESKTOPDIR="$_installdir/share/applications"       
  make
}

package() {
  cd "$pkgname/build"

  make INSTALL_ROOT="$pkgdir" install
}
