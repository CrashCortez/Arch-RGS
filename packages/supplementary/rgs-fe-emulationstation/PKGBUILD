# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-fe-emulationstation
pkgver=2.9.3
pkgrel=1
pkgdesc="A Flexible Emulator Front-End"
arch=('x86_64')
url="https://github.com/RetroPie/EmulationStation"
license=('MIT')
depends=(
  'alsa-lib' 
  'sdl2' 
  'boost-libs' 
  'freeimage' 
  'ttf-dejavu' 
  'glu' 
  'vlc' 
  'libcec'
)
makedepends=(
  'git' 
  'boost' 
  'freetype2' 
  'eigen' 
  'cmake' 
  'mesa' 
  'libsm' 
  'pugixml' 
  'rapidjson'
)
source=("$pkgname::git+https://github.com/retropie/emulationStation.git#tag=v$pkgver")
sha256sums=('SKIP')
_installdir="/opt/archrgs/supplementary/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

prepare() {
  cd "$pkgname"

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
  
  git submodule init
  git submodule update --init --recursive
}

build() {
  cd "$pkgname/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX="$_installdir" 
  make
}

package() {
  cd "$pkgname"

  install -dm755 "${pkgdir}${_installdir%/}"
  install -Dm755 ./{emulationstation,emulationstation.sh,CREDITS.md,GAMELISTS.md,README.md,THEMES.md} "${pkgdir}${_installdir%/}/"
  install -Dm644 ./LICENSE.md "${pkgdir}${_licencedir%/}/LICENSE"
  cp -r ./resources "${pkgdir}${_installdir%/}"
}
