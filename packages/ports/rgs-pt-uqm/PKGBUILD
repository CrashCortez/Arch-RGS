# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-uqm
pkgver=0.7.0
pkgrel=1
pkgdesc="A Port of Star Control 2"
arch=('x86_64')
url="http://sc2.sourceforge.net/"
license=('GPL2')
depends=(
  'glu'
  'libgl'
  'libmikmod' 
  'libvorbis' 
  'sdl_image'
)
makedepends=(
  'imagemagick' 
  'mesa'
)
source=(
  "https://downloads.sourceforge.net/sourceforge/sc2/uqm-${pkgver}-source.tgz"
  "https://downloads.sourceforge.net/sourceforge/sc2/uqm-${pkgver}-content.uqm"
  "https://downloads.sourceforge.net/sourceforge/sc2/uqm-${pkgver}-voice.uqm"
  "https://downloads.sourceforge.net/sourceforge/sc2/uqm-${pkgver}-3domusic.uqm"
  'config.state'
)
noextract=(
  'uqm-${pkgver}-content.uqm' 
  'uqm-${pkgver}-voice.uqm' 
  'uqm-${pkgver}-3domusic.uqm'
)
sha256sums=(
  'a3695c5f7f0be7ec9c0f80ec569907b382023a1fee6e635532bd53b7b53bb221'
  'b8f6db8ba29f0628fb1d5c233830896b19f441aee3744bda671ea264b44da3bf'
  'bcccf801b4ba37594ff6217b292744ea586ee2d447e927804842ccae8b73c979'
  'c57085e64dad4bddf8a679a9aa2adf63f2156d5f6cbabe63af80519033dbcb82'
  '9657d6354178d1a58b04f6f8ff78dc400a0c884d401a12bb89c2372a5d8366ad'
)
_installdir="/opt/archrgs/ports/$pkgname"

prepare() {
  cd "$srcdir/uqm-$pkgver"
  export LDFLAGS="-Wl,--copy-dt-needed-entries"

  mkdir -p ./content/{,addons,packages}
  
  cp ../uqm-$pkgver-content.uqm ./content/packages
  cp ../{uqm-$pkgver-voice.uqm,uqm-$pkgver-3domusic.uqm} ./content/addons
  cp ../config.state ./
  sed -e "/INPUT_install_prefix/ s|replaceme|${pkgdir}${_installdir%/}|" -i config.state && \
  ./build.sh uqm reprocess_config
  
  cd src/uqm/comm/
  for d in */ ; do
    cd $d
    mv strings.h ustrings.h
    cd ..
  done
  find . -type f -name '*.c' | xargs sed -i s/strings.h/ustrings.h/g
  find . -type f -name '*.h' | xargs sed -i s/STRINGS/USTRINGS/g
}

build() {
  cd "$srcdir/uqm-$pkgver"
  
  ./build.sh uqm 
}
  
package() {
  cd "$srcdir/uqm-$pkgver"
  
  install -Dm755 ./uqm -t "${pkgdir}${_installdir%/}/bin/"
  cp -r ./content "${pkgdir}${_installdir%/}"
}
