# Maintainer: V0rt3x667 <al-g0l@outlook.com>
pkgname=rgs-pt-openbor
pkgver=0
pkgrel=1
pkgdesc="2D Side Scrolling Game Engine"
arch=('x86_64')
url="http://www.chronocrash.com"
license=('BSD')
depends=(
  'libpng'
  'libvorbis'
  'libvpx'
  'sdl2_gfx'
  'sdl2_image'
  'sdl2_mixer'
  'yasm'
)
makedepends=('git')
source=(
  "$pkgname::git+https://github.com/DCurrent/openbor"
  'unpack.sh'
)
sha256sums=(
  'SKIP'
  '0f56ea5f12731fdb5848655e1c0be47c'
)
_installdir="/opt/archrgs/ports/$pkgname"
_licencedir="/usr/share/licenses/$pkgname"

pkgver() {
  cd "$pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname/engine" || exit

  #Disable Abort On Warnings & Errors
  sed 's/-Werror//' -i Makefile
  
  #Disable RPATH
  sed 's/-Wl,-rpath,$(LIBRARIES)//' -i Makefile
  
  #Fix Locale Warning
  sed 's|en_US.UTF-8|C|g' -i version.sh
  
  #Convert Icon
  convert -resize 48x48 resources/OpenBOR_Icon_128x128.png ../openbor.png
}

build() {
  cd "$pkgname/engine" || exit
  
  ./version.sh
  make \
    SDKPATH=/usr \
    LNXDEV=/usr/bin \
    BUILD_LINUX=1 \
    GCC_TARGET=$CARCH
    
  cd "$srcdir/$pkgname/tools/borpak/source" || exit
  
  sh ./build.sh linux
}

package() {
  cd "$pkgname/engine" || exit
  
  install -Dm755 ./OpenBOR -t "${pkgdir}${_installdir%/}/bin/"
  install -Dm755 ../tools/borpak/source/borpak -t "${pkgdir}${_installdir%/}/bin/"
  install -Dm755 ../../unpack.sh -t "${pkgdir}${_installdir%/}/bin/"
  install -Dm644 ./LICENSE "${pkgdir}${_licencedir%/}/"
}
