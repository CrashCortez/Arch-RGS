# Maintainer: V0rt3x667 <al-g0l@outlook.com>

pkgname=rgs-pt-openbor
pkgver=6510.dev.r572.g5428eec8
pkgrel=1
pkgdesc="Ultimate 2D side scrolling engine for beat em' ups, shooters, and more!"
arch=('x86_64')
url="http://www.chronocrash.com"
license=('BSD')
depends=('sdl2' 'sdl2_gfx' 'sdl2_image' 'sdl2_mixer' 'libvorbis' 'libpng' 'libvpx' 'yasm')
makedepends=('git')
source=("$pkgname::git+https://github.com/DCurrent/openbor.git"
        'unpack.sh')
md5sums=('SKIP'
         '0f56ea5f12731fdb5848655e1c0be47c')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname/engine

  # disable abort on warnings/errors
  sed 's/-Werror//' -i Makefile
  # disable RPATH
  sed 's/-Wl,-rpath,$(LIBRARIES)//' -i Makefile
  # verbose output
  #sed 's/@$(CC)/$(CC)/' -i Makefile
  # fix a locale warning
  sed 's|en_US.UTF-8|C|g' -i version.sh
  # convert icon
  convert -resize 48x48 resources/OpenBOR_Icon_128x128.png ../openbor.png
}

build() {
    cd $pkgname/engine
    ./version.sh
    make SDKPATH=/usr LNXDEV=/usr/bin BUILD_LINUX=1 GCC_TARGET=$CARCH
    
    cd ../../$pkgname/tools/borpak/source
    sh ./build.sh linux
}

package() {
    cd $pkgname/engine
    
    mkdir -p $pkgdir/opt/archrgs/ports/$pkgname/bin
    mkdir -p $pkgdir/usr/share/licenses/$pkgname
    
    cp ./OpenBOR "$pkgdir/opt/archrgs/ports/$pkgname/bin/"
    cp ../tools/borpak/source/borpak "$pkgdir/opt/archrgs/ports/$pkgname/bin/"
    cp ../../unpack.sh "$pkgdir/opt/archrgs/ports/$pkgname/bin/"
    cp ./LICENSE "$pkgdir/usr/share/licenses/$pkgname/"
}